;Space Invaders clone for TI-83
;to compile e.g. spasm64 -I \spasm\inc invaderz.z80 \spasm\exec\invaderz.8xp
;Backlog:
; BUG: sometimes I blast the sh*t out of a column & it disappears--even tho I haven't yet cleanup up explosions in my code! how?!?!
;......and it seems to cause other columns to glitch somehow. Not sure how it's possible!
; 2DO: account for missile byte shift. maybe use mcol + 5 due to sprite design?
; 2DO: Add animation of sprites, e.g. eyes legs
; 2DO: Sprite collision (player vs invader, player vs bomb)
; 2DO: Count lives lost
; 2DO: Count invaders killed

;Finished:
; 2DO: Allow drop bomb incl adjust brow (on a timer)
; BUG: Can't shoot far right column. I think it's line 306 where I compare to 80d
; item below doesn't work like this anymore--total refactor
; BUG: Good news is coff recomputation is working. Bad news is if it happens in the middle of screen, everybody jumps back left
;......SO when this happens we should compensate by increasing vcol position
;......OR we should do it only at the far left
;......ALSO I hate that explosions cause non-zero column computation. meaning that empty column isn't reflected until next kill.
;......another argument to doing this computation at the margins, not after a kill
; 2DO: Recompute vrightbound based on max coff --refactored & doesn't work like this anymore
; MAYB: Move player in 8px increments instead of smoothly? Enemy will always move smoothly tho!!
;.......tried this and didn't like it.
; 2DO: Recompute coff periodically when an entire left column is 0
; 2DO: Cleanup explosions periodically
; 2DO: Set a flag when an exploded enemy has occurred
; ...after the next run of putSprite, clear any explodes & replace with zero
; 2DO: Add range of motion (i.e. upd leftmost & rightmost) when an enemy is destroyed
; 2DO: Blank the invaders element where enemy was killed so nothing renders in that place
; BUG: For some reason Enter/Clear doesn't quit on RET Z appx line 110
; 2DO: Sprite collision (missile vs invader, 
; 2DO: Accept input, spawn missile
; 2DO: Allow missile animation incl adjust mrow (on a timer)
; 2DO: Look for optys to use JR instead of JP
; 2DO: Consider pcol move my 2px instead of 1px;
; 2DO: Look for optys to use DJNZ in outer loops; just need to use PUSH BC & POP BC to preserve B!
; 2DO: Accept input, adjust pcol left or right
; 2DO: Display player sprite
; 2DO: Move entire fleet of invaders by adjusting vcol (on a timer)
; BUG: When col (px) spans bytes, sprite is being cutoff
; NO: Have a 2nd copy of "invaders" array that points to left-facing sprites
; NO: In case of blanking an element, be sure to blank both left & right versions!
; There won't be right & left facing sprites. This was going to be a trick to avoid blanking the buffer on each draw cycle
; But, it's not going to work due to proximity of sprites and need to OR each rendered sprite against existing buffer contents



.nolist
#include "ti83plus.inc"
#define ProgStart $9D95
.list

screenSize EQU 768


picData EQU appBackupScreen
rawInput EQU appBackupScreen + screenSize


col EQU rawInput + 1
row EQU rawInput + 2
ltpart EQU rawInput + 3
rtpart EQU rawInput + 4
spriteShift EQU rawInput + 5
framect EQU rawInput + 6
vrow EQU rawInput + 7
vmod EQU rawInput + 8
pcol EQU rawInput + 9
prow EQU rawInput + 10
mrow EQU rawInput + 11
;leave 6 bytes for next var
brow EQU rawInput + 17
;leave 6 bytes for next var
enemyCount EQU rawInput + 23
tock EQU rawInput + 24
killFlag EQU rawInput + 25
xcol EQU rawInput + 26
xrow EQU rawInput + 27
bdrop EQU rawInput + 28


.ORG ProgStart - 2

.db t2ByteTok, tAsmCmp

	;given a screen 96 px wide x 64 px tall
	;(in 8x8 terms this is 12 x 8)
	;with 8px per byte, a row (horizontal line) is 12 bytes
	;valid range for sprite upper left (col, row) is (0,0) to (88 px,57 px) or ($58, $38)



	;init player row, col
	LD A, $38
	LD (prow), A
	
	;LD A, $2C
	LD A, $28
	LD (pcol), A

	;init invader (fleet) moving right
	LD A, 1
	LD (vmod), A
	
	;init invader (vrow)
	LD A, 0
	LD (vrow), A
	
	;init missile rows & cols at 0
	;mrow is a pointer to mrow,mcol,mrow0,mcol0,mrow1,mcol1
	LD B, 6
initMrow:
	LD HL, mrow
	LD (HL), 0
	INC HL
	DJNZ initMrow
	
	;init bdrop
	LD HL, bdrop
	LD (HL), 0

	;init numinvaders
	;needs to match invaders array, btw
	;entire game is hard-coded for 11 x 4
	;just an easy way to track game-over condition
	LD HL, enemyCount
	LD (HL), $2C						;44d enemies to start

	;init killFlag
	LD HL, killFlag
	LD (HL), 0

	;init "timer"
	LD A, 0
	PUSH AF								;set initial condition going into mainloop

mainLoop:
	POP AF								;recall A value as a loop counter
	INC A								;increment
	PUSH AF								;save it
	CP 0
	CALL Z, update						;if we've rolled-over 256 to 0 it's time to update & render
										;later I might decouple update from render
	
	;but what is A after calling update??? could be anything right???
	AND %00000111						;equivalent of A mod 8
	CP 0
	JR Z, getInput						;if 0, let's check for input
	
	LD A, (bdrop)
	CP 0
	JR Z, enemyBomb

	JR mainLoop							;repeat

getInput:
	B_CALL(_GetCSC)						;check keypad input
	;CP skUp
	;JP Z, fireMissile
	CP skLeft
	JR Z, playerLeft
	CP skRight 
	JR Z, playerRight
	CP skClear
	JR Z, playerQuit
	JR mainLoop
	
playerQuit:
	B_CALL(_ClrLCDFull)					;clear screen
	LD HL, 0
	LD (PenCol), HL						;init cursor column position
	LD HL, quitMessage
	B_CALL(_PutS)						;OS-specific "output line" function
	B_CALL(_GetKey)						;"input line" function (stores result in A)
	CP $31								;ASCII "1" for Continue <<<<<<<<<<<<<<<<<<<<<<<<<<<< not working
	JR Z, mainLoop
	POP AF								;cleanup last push from mainloop
	RET

playerLeft:
	LD A, (pcol)
	CP 0								;leftmost boundary = 0
	JR Z, mainLoop						;do nothing & keep looping
	LD B, $FC							;-4d
	JR playerMove
	
playerRight:
	LD A, (pcol)
	CP $58								;rightmost boundary = 88d
	JR Z, mainLoop						;do nothing & keep looping
	LD B, 4
	
playerMove:
	ADD A, B							;otherwise g'head and update player col pcol
	LD (pcol), A						;save updated position
	CALL putPlayer						;update the screen buffer
	JR mainLoop							;keep looping

enemyBomb:
	;this will be different than fireMissile
	;in this case, enemies will drop all 3 in a row
	;and not shoot again until all 3 have fallen
	;we work backwards here to drop bombs from lowest invader row

	LD HL, brow + 6				;pointer to end of brow, i.e. bcol3
	PUSH HL						;push brow to stack
	LD HL, invaders + $36		;start from the end cuz we wanna check rows from bottom up
	LD B, $0B					;x11 outerloop
	LD C, 3						;max 3 bomb count
	LD A, (pcol)
	ADD A, 8
	LD D, A						;track pcol + 8 in D as comparison target

_ebEachColumn
	LD A, C
	CP 0						;compare bomb count
	JR Z, _ebNextCol
	
	;bomb count > 0
	PUSH BC
	LD B, 4						;x4 inner loop
	LD E, 4						;bomb rownum counter
	LD A, 0						;invader element flag (0) for compare

__ebEachRow
	CP (HL)						;compare element to zero
	JR NZ, __ebNextRow			;non-zero we just skip
	DEC E						;else decrement rownum counter

__ebNextRow
	DEC HL
	DJNZ __ebEachRow
	
	;resume ebEachColumn
	;HL should now be at vcoff for this column
	LD A, $FF
	CP (HL)						;$FF is flag for empty column
	JR Z, _ebNoBomb
	LD A, D						;stage comparison target from earlier
	
	;we wanna drop a bomb if target > vcoff
	CP (HL)						;compare to vcoff element
	JR Z, _ebYesBomb			;target = vcoff
	JR NC, _ebYesBomb			;target > vcoff
	JR C, _ebNoBomb				;target < vcoff
	
_ebYesBomb
	POP BC						;recall outer count & uncover bcolN
	LD A, (HL)					;current vcoff will become bcol
	EX (SP), HL					;previously we pushed bcolN to the stack
	LD (HL), A					;commit bcol
	DEC HL						;pointing at brow
	PUSH BC						;ugly extraneous push to re-preserve our outer count
	LD A, (vrow)
	LD B, E						;E still holds rownum
	CALL plusTimes8				;compute brow as vrow + (rownum * 8)
	POP BC						;pop our count back & uncover invaders element on stack
	LD (HL), A					;commit brow
	DEC HL						;leave it pointing at next bcol
	EX (SP), HL					;swap bcolN back onto stack, retrieve invaders element
	DEC C						;dec bomb count
	DEC HL						;next rowbottom
	LD A, 1
	LD (bdrop), A				;set bdrop flag
	JR _ebNextCol
	
_ebNoBomb
	POP BC						;recall outer count
	DEC HL						;next rowbottom

_ebNextCol
	DJNZ _ebEachColumn
	POP HL						;pop bcol pointer off the stack

	JP mainLoop


update:
	LD A, (vmod)
	CP 1								;vmod == 1?
	JR Z, _updRight						;yes, so update right
										;else update left

_updLeft
	LD B, $0B							;loop a max of 11d times
	LD HL, invaders

__linner
	;find first non-FF index
	LD A, (HL)							;examine vcoff
	CP $FF								;is it FF?
	JR NZ, __lfound						;not FF, thus found leftmost column
	PUSH BC								;preserve iteration count
	LD B, 0
	LD C, 5
	ADD HL, BC							;skip to next vcoff +5 bytes
	POP BC								;recall iteration count
	DEC B								;decrement count
	LD A, B								;stage count for comparision
	CP 0								;done iterating?
	JR NZ, __linner						;repeat
	
	JR _justUpdate

__lfound
	CP 0								;position is 0?
	JR Z, _toggleDir					;yes - toggle the other direction
	JR _justUpdate						;no - just update in same direction

_updRight
	LD B, $0B
	LD HL, invaders + $32				;search from the right, backwards from +50d
	
__rinner
	;find first non-FF index
	LD A, (HL)
	CP $FF
	JR NZ, __rfound
	PUSH BC
	LD B, $FF
	LD C, $FB							;load BC with -5
	ADD HL, BC
	POP BC								;recall count
	DEC B
	LD A, B
	CP 0
	JR NZ, __rinner
	
	JR _justUpdate
	
__rfound
	CP $58								;position is 88d?
	JR Z, _toggleDir
	JR _justUpdate

_toggleDir
	LD A, (vmod)
	NEG
	LD (vmod), A

_justUpdate
	LD B, $0B
	LD A, (vmod)
	LD D, A								;stage vmod to D
	LD HL, invaders
	
__jinner
	LD A, (HL)
	CP $FF								;FF is a flag for obliterated columns
	JR Z, __updCont						;skip FF
	ADD A, D							;vcoff + vmod
	LD (HL), A							;apply update

__updCont
	PUSH BC
	LD B,0
	LD C,5
	ADD HL, BC
	POP BC
	DJNZ __jinner

render:
	CALL clearGrBuffer
	CALL putPlayer
	;CALL putMissiles
	
	LD A, (bdrop)
	CP 0
	CALL NZ, putBombs					;any non-zero value we'll display whatever's dropping

	LD HL, invaders						;HL must point to invaders before "putInvaders"
	LD A, (vrow)
	LD B, 4								;putInvaders will loop x4 lines of enemies
	CALL putInvaders

	B_CALL(_GrBufCpy)					;finally display the screen buffer!

	RET									;finally return to caller

putPlayer:
	LD IX, player						;loads right-facing version
	LD A, (prow)
	LD (row), A
	LD A, (pcol)
	LD (col), A
	CALL putSprite
	RET

plusTimes8
	;A should hold the base amount (or zero) to be added to
	;BC should contain the *n amount
	ADD A, 8
	DJNZ plusTimes8
	RET

putBombs
	LD B, 3
	LD C, 3								;b counter
	LD HL, brow
	
_pbInner
	;for each row
	LD A, $38							;boundary is brow <= 56d
	CP (HL)
	JR Z, _pbSkip						;A (boundary) = brow, it's ok
	JR NC, _pbSkip						;A (boundary) > brow, it's ok
	JR C, _pbSkip						;A (boundary) < brow, boundary exceeded not ok

_pbPut
	LD A, (HL)							;value of brow
	LD (row), A							;this iteration will render at current location
	ADD A, 2							;+=2 for next iteration
	LD (HL), A							;commit back to RAM
	INC HL								;advance to bcol
	LD A, (HL)
	LD (col), A
	LD IX, bomb
	PUSH HL								;preserve pointer cuz putSprite will lose it
	CALL putSprite
	POP HL								;recall pointer
	JR _pbCont

_pbSkip
	INC HL								;step thru unused bcol for consistency
	DEC C								;dec counter, if we hit zero it'll mean all bombs have fallen

_pbCont
	INC HL								;next brow
	DJNZ _pbInner

	LD A, C
	LD (bdrop), A						;whatever the count; iterpreted elsewhere as 0 drop new bombs; non-zero display existing
	RET

putInvaders:
	;point to invaders
	;read the column offset (vcoff)
	;read the invader style
	;add 8px row offset (vroff)
	;repeat for each row
	;repeat for each column
	
	LD B, $0B							;init 11d columns
	LD HL, invaders
	
_putInvaderCol:
	PUSH BC								;preserve iteration (outer) count
	LD A, (HL)							;get vcoff for this column
	CP $FF								;$FF is flag for empty column
	JR Z, _skipInvader					;is it FF? don't process it
	LD D, A								;else we use D to hold vcoff
	LD A, (vrow)
	LD E, A								;initial vrow will become vroff
	LD B, 4

_putInvader
	INC HL								;next element is sp_sheet offset (spoff)
	PUSH BC								;preserve iteration (inner) count
	LD B, 0
	LD C, (HL)							;stage BC with byte offset
	LD IX, sp_sheet
	ADD IX, BC							;IX now points to correct sprite frame
	PUSH HL								;preserve our place in invaders
	PUSH DE								;preserve our vcoff & vroff
	LD HL, col
	LD (HL), D							;init col for putSprite
	LD HL, row
	LD (HL), E							;init row for putsprite
	CALL putSprite
	POP DE								;recall our vcoff, vroff
	POP HL								;recall our place in invaders
	POP BC								;recall our iteration count (inner)
	LD A, E								;recall vroff
	ADD A, 8							;add 8
	LD E, A								;commit sum to E
	DJNZ _putInvader
	INC HL								;next element is coff
	JR _contInvader

_skipInvader
	LD B, 0
	LD C, 5								;next vcoff is +5 bytes
	ADD HL, BC							;pointer to next vcoff

_contInvader
	POP BC								;recall our iteration count (outer)
	DJNZ _putInvaderCol
	RET

;anim:
	;(framect) must be initialized
	;IX must be pre-loaded with sprite, e.g. LD IX, pacman
	;this works cuz putSprite INCs IX thru each sprite row
	;so last row of frame sets up 1st row of next frame
	;CALL clearGrBuffer					;erase entire buffer every time?? only if we must
	
	;CALL putSprite
	;LD A, (framect)
	;DEC A
	;RET Z
	;LD (framect), A
	;JR anim

putSprite:
	;(row),(col) must be initialized
	;IX must be pre-loaded with sprite, e.g. LD IX, pacman
	;draws an 8x8 sprite
	;looks up row, col values from variables
	;IX must contain pointer to sprite
	;Registers destroyed: A, HL, DE, BC, IX

	;first let's find the byte row!
	;Address should be 12 * row + col
	;only need to do this once for 1st row (we'll just increment thereafter)
	LD A, (row)							;row in px
	LD DE, $0C							;12d
	CALL de_times_a						;result is in HL
	PUSH HL								;save this result
	LD HL, PlotSScreen					;point to screen buffer
	POP BC								;retrieve result
	ADD HL, BC							;add result (row offset) to HL
	
	;next find byte column & bitshift (if any)
	;Byte offset should be col / 8
	;only need to do this once for 1st row (we'll just increment thereafter)
	LD A, (col)							;col in px
	LD D, A								;dividend A
	LD E, 8								;divisor 8
	CALL div_d_e						;quotient (column) to D, remainder (bitshift) to A
	LD (spriteShift), A					;save bitshift to RAM
	LD B, 0								;Zero-out the left half of BC
	LD C, D								;load quotient (column) to C of BC
	ADD HL, BC							;add (column) offset to HL
	
	LD B, 8								;_spriteRow to run 8 times via DJNZ

_spriteRow:
	;expect IX to contain ptr to sprite
	;draw a 1-byte horizontal row of a sprite
	;first load the spriterow into memory
	PUSH BC								;preserve B for DJNZ later
	LD A, (IX)							;load sprite from IX to A
	LD (ltpart), A						;store a copy of sprite in leftpart
	LD (rtpart), A						;store a copy of sprite in rightpart
	LD A, (spriteShift)					;recall spriteshift
	CP 0								;compare A to zero
	JR Z, _leftByte						;if A is zero JP to leftByte, don't do any bit shifting

	;handle situation where x spans 2 bytes
	;A contains the shift offset from above
	LD B, A								;prepare to loop B times per offset bits
	LD C, A								;will use offset for masking momentarily
	PUSH HL								;preserve byte location
	CALL _shiftLoop						;loop will run B times to shift bits
	PUSH HL								;preserve rtpart
	LD HL, myMask						;NOTE: B is zero coming out of loop; C holds offset, so...
	ADD HL, BC							;...HL now points to correct mask for this offset
	LD A, (HL)							;stage the mask value in A
	LD B, A								;Load B with mask value
	POP HL								;retrieve rtpart loc
	LD A, (HL)							;Load A with rtpart value
	AND B								;apply mask
	LD (rtpart), A						;rtpart is now masked
	JR _rightByte
	
_shiftLoop:
	LD HL, ltpart
	SRL (HL)							;shift right
	LD HL, rtpart
	RR (HL)								;rotate right
	DJNZ _shiftLoop
	RET
	
_rightByte:
	POP HL								;retrieve byte location
	INC HL								;move one byte ahead for right byte
	LD A, (rtpart)						;Stage A with right half of sprite bits
	OR (HL)								;OR with whatever's already there
	LD (HL), A							;set byte value; i.e. rtpart + existing
	DEC HL								;drop back to original SScreen location 
	
_leftByte:
	;for no bitshift, HL already contains correct SScreen loc, no need to pop
	LD A, (ltpart)						;Stage A with left half of sprite bits
	OR (HL)								;OR with whatever's already there
	LD (HL), A							;set byte value, e.g. ltpart + existing
	
_nextByte:
	LD B, 0								;B of BC to 0
	LD C, $0C							;C of BC to 12d
	ADD HL, BC							;increment HL by 12 for next row
	INC IX								;increment ptr to spriteRow

	POP BC
	DJNZ _spriteRow

	RET

clearGrBuffer:
	LD HL, PlotSScreen
	LD DE, PlotSScreen + 1
	LD BC, screenSize - 1
	LD (HL), 0
	LDIR
	RET

;fast multiplication route from http://tutorials.eeems.ca/ASMin28Days/lesson/day15.html
;HL = DE * A

de_times_a:
	LD HL, 0							;HL to store the product
	LD B, 8								;8-bits to check

_loop0:
	RRCA								;check LSB of A
	JR NC, _skip						;if zero, skip addition
	ADD HL, DE

_skip:
	SLA E								;shift DE one bit left
	RL D
	DJNZ _loop0
	RET

;division routine from wikiti.brandonw.net
;divide d by e, quotient result in d, remainder in a

div_d_e:
	XOR A
	LD B, 8

_loop1:
	SLA D
	RLA
	CP E
	JR C, $+4
	SUB E
	INC D

	DJNZ _loop1

	RET

sp_sheet:
;empty i.e. vanquished invader
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000

;style alpha frame 1
.DB %00000000
.DB %01100011
.DB %00010100
.DB %00111110
.DB %01101011
.DB %01111111
.DB %01000001
.DB %00110110

;style alpha frame 2
.DB %00000000
.DB %01100011
.DB %00010100
.DB %00111110
.DB %01101011
.DB %01111111
.DB %01010101
.DB %00100010

;style beta frame 1
.DB %00000000
.DB %00011100
.DB %00111110
.DB %01111111
.DB %01101101
.DB %01111111
.DB %00101010
.DB %01001001

;style beta frame 2
.DB %00000000
.DB %00011100
.DB %00111110
.DB %01111111
.DB %01011011
.DB %01111111
.DB %00101010
.DB %01001001

;style exploded (40d, $28)
.DB %00000000
.DB %01001001
.DB %00101010
.DB %00000000
.DB %01100011
.DB %00000000
.DB %00101010
.DB %01001001

invaders:
;  coff, styles...
.DB   0, $10, $20, $10, $20
.DB   8, $10, $20, $10, $20
.DB $10, $10, $20, $10, $20
.DB $18, $10, $20, $10, $20
.DB $20, $10, $20, $10, $20
.DB $28, $10, $20, $10, $20
.DB $30, $10, $20, $10, $20
.DB $38, $10, $20, $10, $20
.DB $40, $10, $20, $10, $20
.DB $48, $10, $20, $10, $20
.DB $50, $10, $20, $10, $20

player:
;right facing
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00010100
.DB %00011100
.DB %00111110
.DB %01111111
.DB %01111111

missile:
.DB %00000000
.DB %00001000
.DB %00001000
.DB %00001000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000

explosion:
.DB %00000000
.DB %01001001
.DB %00101010
.DB %00000000
.DB %01100011
.DB %00000000
.DB %00101010
.DB %01001001

bomb:
.DB %00000000
.DB %00000000
.DB %00011100
.DB %00111110
.DB %00111110
.DB %00111110
.DB %00011100
.DB %00000000

;why not write these as binary masks since that's what they're used for??? <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< LOOK
myMask:
.DB 0, $80, $C0, $E0, $F0, $F8, $FC, $FE, $FF

quitMessage:
.DB "Press 1 to Continue. Any other key to Quit.",0

.END