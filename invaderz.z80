;Space Invaders clone for TI-83
;Backlog:
; 2DO: account for missile byte shift. maybe use mcol + 5 due to sprite design?
; MAYB: Move player in 8px increments instead of smoothly? Enemy will always move smoothly tho!!
; 2DO: Add animation of sprites, e.g. eyes legs
; 2DO: Allow drop bomb incl adjust brow (on a timer)
; 2DO: Sprite collision (player vs invader, player vs bomb)
; 2DO: Count lives lost
; 2DO: Count invaders killed

;Finished:
; 2DO: Set a flag when an exploded enemy has occurred
; ...after the next run of putSprite, clear any explodes & replace with zero
; 2DO: Add range of motion (i.e. upd leftmost & rightmost) when an enemy is destroyed
; 2DO: Blank the invaders element where enemy was killed so nothing renders in that place
; BUG: For some reason Enter/Clear doesn't quit on RET Z appx line 110
; 2DO: Sprite collision (missile vs invader, 
; 2DO: Accept input, spawn missile
; 2DO: Allow missile animation incl adjust mrow (on a timer)
; 2DO: Look for optys to use JR instead of JP
; 2DO: Consider pcol move my 2px instead of 1px;
; 2DO: Look for optys to use DJNZ in outer loops; just need to use PUSH BC & POP BC to preserve B!
; 2DO: Accept input, adjust pcol left or right
; 2DO: Display player sprite
; 2DO: Move entire fleet of invaders by adjusting vcol (on a timer)
; BUG: When col (px) spans bytes, sprite is being cutoff
; NO: Have a 2nd copy of "invaders" array that points to left-facing sprites
; NO: In case of blanking an element, be sure to blank both left & right versions!
; There won't be right & left facing sprites. This was going to be a trick to avoid blanking the buffer on each draw cycle
; But, it's not going to work due to proximity of sprites and need to OR each rendered sprite against existing buffer contents



.nolist
#include "ti83plus.inc"
#define ProgStart $9D95
.list

screenSize EQU 768


picData EQU appBackupScreen
rawInput EQU appBackupScreen + screenSize


col EQU rawInput + 1
row EQU rawInput + 2
ltpart EQU rawInput + 3
rtpart EQU rawInput + 4
spriteShift EQU rawInput + 5
framect EQU rawInput + 6
vcol EQU rawInput + 7
vrow EQU rawInput + 8
vmod EQU rawInput + 9
pcol EQU rawInput + 10
prow EQU rawInput + 11
mrow EQU rawInput + 12
;leave 6 bytes for next var
ileftmost EQU rawInput + 18
irightmost EQU rawInput + 19
vrightbound EQU rawInput + 20
enemyCount EQU rawInput + 21
rowoff EQU rawInput + 22
explodedFlag EQU rawInput + 23


.ORG ProgStart - 2

.db t2ByteTok, tAsmCmp

	;given a screen 96 px wide x 64 px tall
	;(in 8x8 terms this is 12 x 8)
	;with 8px per byte, a row (horizontal line) is 12 bytes
	;valid range for sprite upper left (col, row) is (0,0) to (88 px,57 px) or ($58, $38)



	;init player row, col
	LD A, $38
	LD (prow), A
	
	;LD A, $2C
	LD A, $28
	LD (pcol), A

	;init invader (fleet) left column
	LD A, 3
	LD (vcol), A
	
	;init invader (fleet) moving right
	LD A, 1
	LD (vmod), A
	
	;init invader (fleet) top row
	LD A, 0
	LD (vrow), A

	;init missile rows & cols at 0
	;mrow is a pointer to mrow,mcol,mrow0,mcol0,mrow1,mcol1
	LD B, 6
initMrow:
	LD HL, mrow
	LD (HL), 0
	INC HL
	DJNZ initMrow

	;init ileftmost, irightmost, vrightbound
	LD HL, ileftmost
	LD (HL), 0
	LD HL, irightmost
	LD (HL), $0A						;10d is zero-based right column index
	LD HL, vrightbound
	LD (HL), 7

	;init numinvaders
	;needs to match invaders array, btw
	;entire game is hard-coded for 11 x 4
	;just an easy way to track game-over condition
	LD HL, enemyCount
	LD (HL), $2C						;44d enemies to start

	;init explodedFlag
	LD HL, explodedFlag
	LD (HL), 0

	;init "timer"
	LD A, 0
	PUSH AF								;set initial condition going into mainloop

mainLoop:
	POP AF								;recall A value as a loop counter
	INC A								;increment
	PUSH AF								;save it
	CP 0
	CALL Z, update						;if we've rolled-over 256 to 0 it's time to update & render
										;later I might decouple update from render
	AND %00000111						;equivalent of A mod 8
	CP 0
	JR Z, getInput						;if 0, let's check for input
	JR mainLoop							;repeat

getInput:
	B_CALL(_GetCSC)						;check keypad input
	CP skUp
	JR Z, fireMissile
	CP skLeft
	JR Z, playerLeft
	CP skRight 
	JR Z, playerRight
	CP skClear
	JP Z, playerQuit
	JR mainLoop
	
playerQuit:
	B_CALL(_ClrLCDFull)					;clear screen
	LD HL, 0
	LD (PenCol), HL						;init cursor column position
	LD HL, quitMessage
	B_CALL(_PutS)						;OS-specific "output line" function
	B_CALL(_GetKey)						;"input line" function (stores result in A)
	CP $31								;ASCII "1" for Continue <<<<<<<<<<<<<<<<<<<<<<<<<<<< not working
	JR Z, mainLoop
	POP AF								;cleanup last push from mainloop
	RET

playerLeft:
	LD A, (pcol)
	CP 0								;leftmost boundary = 0
	JR Z, mainLoop						;do nothing & keep looping
	;LD B, $FE							;-2d
	LD B, $F8							;-8d
	JR playerMove
	
playerRight:
	LD A, (pcol)
	CP $58								;rightmost boundary = 88d
	JR Z, mainLoop						;do nothing & keep looping
	LD B, 8
	
playerMove:
	ADD A, B							;otherwise g'head and update player col pcol
	LD (pcol), A						;save updated position
	CALL putPlayer						;update the screen buffer
	JR mainLoop							;keep looping

fireMissile:
	LD HL, mrow							;missile row, zer0 indicates inactive flag
	LD A, 3								;we have 3 "slots" or silos available for missiles

_nextSilo:
	PUSH AF								;save our silo count
	LD A, (HL)
	CP 0								;is this silo ready? == 0?
	JR Z, _setMissile					;if so, launch a missile here!
	INC HL								;inc 1x brings us to mcol which we don't need right now
	INC HL								;inc 2x to get next mrow
	POP AF								;recall our silo count
	DEC A								;decrement silo count
	CP 0
	JR NZ, _nextSilo					;not zero so go next silo
	JR mainLoop

_setMissile:
	POP AF								;cleanup the previous push
	LD (HL), $38						;init mrow at line 56d
	INC HL								;advance to mcol
	LD A, (pcol)						;stage pcol for...
	LD (HL), A							;init mcol at pcol
	JR mainLoop

update:
	;move invaders right/left
	LD A, (vrightbound)					;stage vrightbound in A
	LD B, A								;standby vrightbound in B for compare in a moment
	LD A, (vcol)
	CP 0								;leftmost boundary is always Zero 0px
	JR Z, _toggleDir
	CP B								;vrightbound is 88 - ((rightmost - leftmost) * 8)
	JR NZ, _justUpdate

_toggleDir:
	LD A, (vmod)
	NEG
	LD (vmod), A

_justUpdate:
	LD A, (vmod)
	LD B, A
	LD A, (vcol)
	ADD A, B
	LD (vcol), A

render:
	CALL clearGrBuffer
	CALL putPlayer
	CALL putMissiles

	LD HL, invaders						;HL must point to invaders before "putInvaders"
	LD A, (vrow)
	LD (row), A							;row must be init'd to vrow before "putInvaders"
	LD B, 4								;putInvaders will loop x4 lines of enemies
	CALL putInvaders

	B_CALL(_GrBufCpy)					;finally display the screen buffer!

	RET									;finally return to caller

putPlayer:
	LD IX, player						;loads right-facing version
	LD A, (prow)
	LD (row), A
	LD A, (pcol)
	LD (col), A
	CALL putSprite
	RET

putMissiles:
	LD HL, mrow							;pointer to mrow array (3 elements of 2b each)
	LD B, 3

_eachMissile:
	PUSH BC								;save our B location for DJNZ
	LD A, (HL)							;mrow
	CP 0								;is this missile in flight? Zer0 = No
	PUSH HL								;save our pointer before we call updMissile
	CALL NZ, _updMissile				;Non-zero = Yes
	POP HL								;whether we called updMissile or not, get our ptr back
	INC HL
	INC HL								;2x INC gets us to next mrow
	POP BC								;pop our counter
	DJNZ _eachMissile
	RET

_updMissile:
										;HL already points to current mrow index
										;A already has (HL) value i.e. mrow value
	SUB 4								;move missile -4px (upward on screen)
	LD (HL), A							;commit the update to mrow
	RET Z								;if we're at 0 - exit
	LD (row), A							;init (row) for eventual _putMissile
	
;begin collision check
	PUSH HL								;save mrow pointer
	
	;#1 get byte row of the missile
	LD D, A								;A already holds updated mrow
	LD E, 8
	CALL div_d_e						;divide mcol / 8 to get byte row

_checkRows
	;every row is the same so just use roffs of first column
	LD HL, invaders
	LD B, 4
	
_checkRow
	;#2 get byte row(s) of invader(s)
	;loop over 4x rows until matched
	PUSH DE								;save #1 quotient again
	INC HL								;first element is sprite style
	INC HL								;next is roff
	LD A, (vrow)
	LD D, (HL)
	ADD A, D							;vrow + roff
	LD D, A
	LD E, 8
	CALL div_d_e						;divide (vrow + roff) / 8 to get row
	LD A, D								;stage quotient in accumulator
	POP DE								;recall our quotient from #1
	CP D								;compare quotients
	JR Z, _checkCols
	DEC B
	LD A, 0
	CP B
	JR Z, _skipCols						;no match found
	JR _checkRow
	
_checkCols
	;#3 get byte column of the missile
	POP HL								;restore mrow pointer
	INC HL								;get mcol pointer
	LD A, (HL)							;get mcol value
	LD (col), A							;init (col) for eventual _putMissile
	LD D, A
	LD E, 8
	CALL div_d_e						;divide mcol / 8 to get byte column

	LD HL,invaders
	LD B, $0B
	
_checkCol
	;#4 find invader(s) in this column, if any
	;loop over columns until matched
	PUSH DE								;preserve our quotient from #3
	PUSH BC								;preserve our rowcount (destroyed below by ADD HL,BC & div_d_e)
	LD A, (vcol)
	LD D, (HL)
	ADD A, D							;vcol + coff
	LD D, A
	LD E, 8
	CALL div_d_e						;divide (vcol + coff) / 8 to get byte column
	LD A, D								;stage quotient in accumulator
	POP DE								;recall #2 quotient
	CP D								;compare quotients
	JR Z, _collision
	LD B,0
	LD C,9
	ADD HL,BC							;skip 9 bytes to next column
	POP BC								;recall our rowcount
	DEC B
	LD A, 0
	CP B
	JR Z, _putMissile					;no match found
	JR _checkCols

_collision
	;at this byte

_skipCols
	POP HL								;restore mrow pointer
	INC HL								;get mcol pointer
	LD A, (HL)
	LD (col), A

_putMissile
	LD IX, missile
	CALL putSprite
	RET									;return to caller which was _eachMissile via _updMissile

putInvaders:
	;point to invaders
	;read the column offset (coff) & add it to vcol
	;read the invader style
	;read the row offset (roff) & add it to vrow
	;repeat from invader style
	;later we will dec coff by -8 everytime a leftmost column is obliterated
	;vrightbound will be 7 + coff of last column that isn't empty
	
	;(vcol, vrow) must be initialized
	
	LD B, $0B							;init 11d columns
	LD HL, invaders
	
_putInvaderCol:
	LD A, (vcol)
	LD D, (HL)							;first element is coff
	ADD A, D							;vcol + coff
	LD D, A								;commit sum to D
	PUSH BC								;preserve iteration (outer) count
	LD B, 4

_putInvader
	INC HL								;next element is byte offset of sp_sheet
	PUSH BC								;preserve iteration (inner) count
	LD B, 0
	LD C, (HL)							;stage BC with byte offset
	LD IX, sp_sheet
	ADD IX, BC							;IX now points to correct sprite frame
	INC HL								;next element is roff
	LD A, (vrow)
	LD E, (HL)							;roff
	ADD A,E								;vrow + roff
	LD E, A								;commit sum to E
	PUSH HL								;preserve our place in invaders
	PUSH DE								;preserve our vcol + coff, don't care about E
	LD HL, col
	LD (HL), D							;init col for putSprite
	LD HL, row
	LD (HL), E							;init row for putsprite
	CALL putSprite
	POP DE								;recall our vcol + coff
	POP HL								;recall our place in invaders
	POP BC								;recall our iteration count (inner)
	DJNZ _putInvader
	
	POP BC
	INC HL								;next element is coff
	DJNZ _putInvaderCol
	RET
	
;anim:
	;(framect) must be initialized
	;IX must be pre-loaded with sprite, e.g. LD IX, pacman
	;this works cuz putSprite INCs IX thru each sprite row
	;so last row of frame sets up 1st row of next frame
	;CALL clearGrBuffer					;erase entire buffer every time?? only if we must
	
	;CALL putSprite
	;LD A, (framect)
	;DEC A
	;RET Z
	;LD (framect), A
	;JR anim

putSprite:
	;(row),(col) must be initialized
	;IX must be pre-loaded with sprite, e.g. LD IX, pacman
	;draws an 8x8 sprite
	;looks up row, col values from variables
	;IX must contain pointer to sprite
	;Registers destroyed: A, HL, DE, BC, IX

	;first let's find the byte row!
	;Address should be 12 * row + col
	;only need to do this once for 1st row (we'll just increment thereafter)
	LD A, (row)							;row in px
	LD DE, $0C							;12d
	CALL de_times_a						;result is in HL
	PUSH HL								;save this result
	LD HL, PlotSScreen					;point to screen buffer
	POP BC								;retrieve result
	ADD HL, BC							;add result (row offset) to HL
	
	;next find byte column & bitshift (if any)
	;Byte offset should be col / 8
	;only need to do this once for 1st row (we'll just increment thereafter)
	LD A, (col)							;col in px
	LD D, A								;dividend A
	LD E, 8								;divisor 8
	CALL div_d_e						;quotient (column) to D, remainder (bitshift) to A
	LD (spriteShift), A					;save bitshift to RAM
	LD B, 0								;Zero-out the left half of BC
	LD C, D								;load quotient (column) to C of BC
	ADD HL, BC							;add (column) offset to HL
	
	LD B, 8								;_spriteRow to run 8 times via DJNZ

_spriteRow:
	;expect IX to contain ptr to sprite
	;draw a 1-byte horizontal row of a sprite
	;first load the spriterow into memory
	PUSH BC								;preserve B for DJNZ later
	LD A, (IX)							;load sprite from IX to A
	LD (ltpart), A						;store a copy of sprite in leftpart
	LD (rtpart), A						;store a copy of sprite in rightpart
	LD A, (spriteShift)					;recall spriteshift
	CP 0								;compare A to zero
	JR Z, _leftByte						;if A is zero JP to leftByte, don't do any bit shifting

	;handle situation where x spans 2 bytes
	;A contains the shift offset from above
	LD B, A								;prepare to loop B times per offset bits
	LD C, A								;will use offset for masking momentarily
	PUSH HL								;preserve byte location
	CALL _shiftLoop						;loop will run B times to shift bits
	PUSH HL								;preserve rtpart
	LD HL, myMask						;NOTE: B is zero coming out of loop; C holds offset, so...
	ADD HL, BC							;...HL now points to correct mask for this offset
	LD A, (HL)							;stage the mask value in A
	LD B, A								;Load B with mask value
	POP HL								;retrieve rtpart loc
	LD A, (HL)							;Load A with rtpart value
	AND B								;apply mask
	LD (rtpart), A						;rtpart is now masked
	JR _rightByte
	
_shiftLoop:
	LD HL, ltpart
	SRL (HL)							;shift right
	LD HL, rtpart
	RR (HL)								;rotate right
	DJNZ _shiftLoop
	RET
	
_rightByte:
	POP HL								;retrieve byte location
	INC HL								;move one byte ahead for right byte
	LD A, (rtpart)						;Stage A with right half of sprite bits
	OR (HL)								;OR with whatever's already there
	LD (HL), A							;set byte value; i.e. rtpart + existing
	DEC HL								;drop back to original SScreen location 
	
_leftByte:
	;for no bitshift, HL already contains correct SScreen loc, no need to pop
	LD A, (ltpart)						;Stage A with left half of sprite bits
	OR (HL)								;OR with whatever's already there
	LD (HL), A							;set byte value, e.g. ltpart + existing
	
_nextByte:
	LD B, 0								;B of BC to 0
	LD C, $0C							;C of BC to 12d
	ADD HL, BC							;increment HL by 12 for next row
	INC IX								;increment ptr to spriteRow

	POP BC
	DJNZ _spriteRow

	RET

clearGrBuffer:
	LD HL, PlotSScreen
	LD DE, PlotSScreen + 1
	LD BC, screenSize - 1
	LD (HL), 0
	LDIR
	RET

;fast multiplication route from http://tutorials.eeems.ca/ASMin28Days/lesson/day15.html
;HL = DE * A

de_times_a:
	LD HL, 0							;HL to store the product
	LD B, 8								;8-bits to check

_loop0:
	RRCA								;check LSB of A
	JR NC, _skip						;if zero, skip addition
	ADD HL, DE

_skip:
	SLA E								;shift DE one bit left
	RL D
	DJNZ _loop0
	RET

;division routine from wikiti.brandonw.net
;divide d by e, quotient result in d, remainder in a

div_d_e:
	XOR A
	LD B, 8

_loop1:
	SLA D
	RLA
	CP E
	JR C, $+4
	SUB E
	INC D

	DJNZ _loop1

	RET

sp_sheet:
;empty i.e. vanquished invader
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000

;style alpha frame 1
.DB %00000000
.DB %01100011
.DB %00010100
.DB %00111110
.DB %01101011
.DB %01111111
.DB %01000001
.DB %00110110

;style alpha frame 2
.DB %00000000
.DB %01100011
.DB %00010100
.DB %00111110
.DB %01101011
.DB %01111111
.DB %01010101
.DB %00100010

;style beta frame 1
.DB %00000000
.DB %00011100
.DB %00111110
.DB %01111111
.DB %01101101
.DB %01111111
.DB %00101010
.DB %01001001

;style beta frame 2
.DB %00000000
.DB %00011100
.DB %00111110
.DB %01111111
.DB %01011011
.DB %01111111
.DB %00101010
.DB %01001001

;style exploded (40d, $28)
.DB %00000000
.DB %01001001
.DB %00101010
.DB %00000000
.DB %01100011
.DB %00000000
.DB %00101010
.DB %01001001

invaders:
;  coff, styl, roff, styl, roff, styl, roff, style, roff
.DB   0,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB   8,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $10,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $18,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $20,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $28,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $30,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $38,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $40,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $48,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18
.DB $50,  $10,    0,  $20,    8,  $10,  $10,   $20,  $18

player:
;right facing
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00010100
.DB %00011100
.DB %00111110
.DB %01111111
.DB %01111111

missile:
.DB %00000000
.DB %00001000
.DB %00001000
.DB %00001000
.DB %00000000
.DB %00000000
.DB %00000000
.DB %00000000

explosion:
.DB %00000000
.DB %01001001
.DB %00101010
.DB %00000000
.DB %01100011
.DB %00000000
.DB %00101010
.DB %01001001

;why not write these as binary masks since that's what they're used for??? <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< LOOK
myMask:
.DB 0, $80, $C0, $E0, $F0, $F8, $FC, $FE, $FF

quitMessage:
.DB "Press 1 to Continue. Any other key to Quit.",0

.END